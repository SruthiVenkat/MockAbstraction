// ./doop -a context-insensitive -i ~/hacking/MockAbstraction/Benchmarks/microbenchmark/target/payroll-test-0.0.1-SNAPSHOT.jar -i ~/hacking/MockAbstraction/Benchmarks/microbenchmark/target/payroll-test-0.0.1-SNAPSHOT-tests.jar -i ~/hacking/MockAbstraction/Benchmarks/microbenchmark/mvn_dependencies/ --id context-insensitive-with-mocks --souffle-jobs 32 --main ca.liang.RootDriver --extra-logic souffle-logic/analyses/mocks/mocks.dl

#include "../../basic/type-hierarchy.dl"
#include "../../basic/method-lookup.dl"

// m: Method
.decl MockSourceMethod(m: Method)
MockSourceMethod("<org.easymock.EasyMock: java.lang.Object createMock(java.lang.Class)>").
MockSourceMethod("<org.mockito.Mockito: java.lang.Object mock(java.lang.Class)>").
MockSourceMethod("<org.powermock.api.mockito.PowerMockito: java.lang.Object mock(java.lang.Class)>").
MockSourceMethod("<org.mockito.Mockito: java.lang.Object verify(java.lang.Object,org.mockito.verification.VerificationMode)>").
MockSourceMethod("<org.mockito.internal.MockitoCore: java.lang.Object verify(java.lang.Object)>").
MockSourceMethod("<org.mockito.internal.InOrderImpl: java.lang.Object verify(java.lang.Object)>").
MockSourceMethod("<org.mockito.internal.MockitoCore: java.lang.Object verify(java.lang.Object,org.mockito.verification.VerificationMode)>").
MockSourceMethod("<org.mockito.internal.InOrderImpl: java.lang.Object verify(java.lang.Object,org.mockito.verification.VerificationMode)>").

.decl MockFieldAnnotation(a: Annotation)
MockFieldAnnotation("org.mockito.Mock").

// TODO handle addAll
.decl CollectionPutMethod(m:Method)
CollectionPutMethod("<java.util.Collection: boolean add(java.lang.Object)>").
CollectionPutMethod(m) :-
  CollectionPutMethod(m0),
  MethodOverridesOther(m, m0).

.decl CollectionGetMethod(m:Method)
CollectionGetMethod("<java.util.List: java.lang.Object get(int)>").
CollectionGetMethod(m) :-
  CollectionGetMethod(m0),
  MethodOverridesOther(m, m0).

.decl CallGraphEdge(c1:symbol, invocation:symbol, c2:symbol, meth:symbol)
.input CallGraphEdge(IO="file", filename="CallGraphEdge.csv", delimiter="\t")

// mi: MethodInvocation
.decl callsMockSource(mi: MethodInvocation)
callsMockSource(mi) :-
  MockSourceMethod(ms),
  CallGraphEdge(_, mi, _, ms).
.output callsMockSource

// v:Var
.decl isMockVar(v: Var)
isMockVar(v) :-
  AssignReturnValue(mi, v),
  callsMockSource(mi).
isMockVar(v) :-
  isMockVar(from),
  AssignCast(_ /* type */, from, v, _ /* inmethod */).
isMockVar(v) :-
  isMockVar(v1),
  AssignLocal(v1, v, _).
isMockVar(v) :-
  LoadInstanceField(_, field, v, _),
  fieldContainsMock(field).
isMockVar(v) :-
// v = c.get(_);
  isCollectionLocalThatContainsMocks(c, _),
  CollectionGetMethod(get),
  VirtualMethodInvocation(mi, get, _),
  VirtualMethodInvocation_Base(mi, c),
  AssignReturnValue(mi, v).
isMockVar(v) :-
// v = c[_];
  isArrayLocalThatContainsMocks(c, _),
  LoadArrayIndex(c, v, _).
.output isMockVar

.decl isCollectionLocalThatContainsMocks(c:Var, inmethod:symbol)
// c.add(mv)
isCollectionLocalThatContainsMocks(c, inmethod) :-
  CollectionPutMethod(put),
  VirtualMethodInvocation(mi, put, inmethod),
  VirtualMethodInvocation_Base(mi, c),
  ActualParam(0, mi, mv),
  isMockVar(mv).
isCollectionLocalThatContainsMocks(c, inmethod) :-
  isCollectionFieldThatContainsMocks(f),
  LoadInstanceField(_, f, c, inmethod).
isCollectionLocalThatContainsMocks(c, inmethod) :-
  isCollectionLocalThatContainsMocks(c1, inmethod),
  AssignLocal(c1, c, inmethod).
.output isCollectionLocalThatContainsMocks

.decl isCollectionFieldThatContainsMocks(f:Field)
// base.f = c where c is a CollectionLocalThatContainsMocks -> f is a field that contains mocks
isCollectionFieldThatContainsMocks(f) :-
  isCollectionLocalThatContainsMocks(c, _),
  StoreInstanceField(c, _, f, _).
.output isCollectionFieldThatContainsMocks

.decl isArrayLocalThatContainsMocks(c:Var, inmethod:symbol)
// c[idx] = mv @ insn
isArrayLocalThatContainsMocks(c, inmethod) :-
  StoreArrayIndex(mv, c, inmethod),
  isMockVar(mv).
// c = _.f, f contains mocks
isArrayLocalThatContainsMocks(c, inmethod) :-
  isArrayFieldThatContainsMocks(f),
  LoadInstanceField(_, f, c, inmethod).
isArrayLocalThatContainsMocks(c, inmethod) :-
  isArrayLocalThatContainsMocks(c1, inmethod),
  AssignLocal(c1, c, inmethod).
.output isArrayLocalThatContainsMocks

.decl isArrayFieldThatContainsMocks(f:Field)
// arr = _.f <- (2) propagates so that field f is a field that contains mocks
// arr[i] = mv <- (1) marks arr as an arraylocalthatcontainsmocks
isArrayFieldThatContainsMocks(f) :-
  isArrayLocalThatContainsMocks(arr, _),
  LoadInstanceField(_, f, arr, _).
.output isArrayFieldThatContainsMocks

.decl isMockInvocation(mi: MethodInvocation, signature:Method, inMethod:Method, line: number, receiver: symbol)
isMockInvocation(mi, signature, inmethod, line, receiver) :-
  _MethodInvocation_Line(mi, line),
  VirtualMethodInvocation(mi, signature, inmethod),
  VirtualMethodInvocation_Base(mi, receiver),
  isMockVar(receiver).
.output isMockInvocation

.decl fieldContainsMock(signature: Field)
fieldContainsMock(signature) :-
  StoreInstanceField(from, _, signature, _),
  isMockVar(from).
fieldContainsMock(signature) :-
  Field_Annotation(signature, a),
  MockFieldAnnotation(a).
.output fieldContainsMock
